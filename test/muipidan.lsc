Feature Type(*cons, vowel)
Feature Place(lab, apic, vel, glot) => [cons]
Feature Manner(stop, fric, sonor) => [cons]
Feature Nasal(*unnas, nas) => [cons]
Feature Breath(unvcd, vcd, aspir, eject) => [cons]
Feature Height(high, low)
Feature Depth(front, cent, back)
Feature Rounding(*unrnd, rnd)
Feature Stress(*unstr, str) => [vowel]

Diacritic ˈ (before) [str]
Diacritic ʼ [eject]
Diacritic ʰ [aspir]
Diacritic ̥  [unvcd]

Symbol p [stop unvcd lab]
Symbol t [stop unvcd apic]
Symbol k [stop unvcd vel]
Symbol ʔ [stop unvcd glot]
Symbol b [stop vcd lab]
Symbol d [stop vcd apic]
Symbol g [stop vcd vel]
Symbol ᵐb [stop nas vcd lab]
Symbol ⁿd [stop nas vcd apic]
Symbol ᵑg [stop nas vcd vel]
Symbol f [fric lab]
Symbol s [fric apic]
Symbol x [fric vel]
Symbol h [fric glot]
Symbol m [sonor nas vcd lab]
Symbol n [sonor nas vcd apic]
Symbol l [sonor apic]
Symbol j [sonor high front unrnd]
Symbol w [sonor high back rnd]

Symbol i [vowel high front]
Symbol e [vowel low front]
Symbol y [vowel high front rnd]
Symbol ø [vowel low front rnd]
Symbol ə [vowel high cent]
Symbol a [vowel low cent]
Symbol ɯ [vowel high back]
Symbol ɤ [vowel low back]
Symbol u [vowel high back rnd]
Symbol o [vowel low back rnd]

Deromanizer:
    c => x
    ' => ʔ
    á => ˈa
    é => ˈe
    í => ˈi
    ó => ˈo
    ú => ˈu

first-syllable-stress [vowel]:
    [] => [str] / $ _ [unstr]+ $

syncope [vowel]:
    [] => * / {_ [str], [str] _ []}

# We now have predictable word-initial stress
remove-stress:
    [str] => [unstr]

consonant-coalescence:
    [stop !glot] [stop] => [eject] * / _ [vowel]
    [stop !glot] [fric] => [aspir] * / _ [vowel]
    [nas] [stop] => * [nas vcd]
    [fric] [nas] => * [unvcd]
    [nas] [fric] => [unvcd] *

stop-voicing:
    [stop !glot !eject !aspir] => [vcd] / [vowel] _ [vowel]

cluster-reduction:
    [nas] => * / $ _ [fric]
    l => * / $ _ [cons]
    [stop] => * / _ [stop]
    n => l / [stop] _
    [fric] => * / _ [stop]

nasal-assimilation:
    [nas] => [$Place] / _ [cons $Place]
    n => l / _ l

glottal-loss:
    [glot] => *
    [vowel high] => [cons sonor] / {_ [vowel], [vowel] _}

vowel-harmony-by-height [vowel] propagate:
    [vowel] => [$Height] / [vowel $Height] _

final-vowel-loss:
    [vowel] => * / [vowel] [cons !stop] _ $
    {e, ə, u} => {i, a, o} / [vowel] [cons]* _ $

coda-l:
    l => j / _ [cons] // _ [sonor unnas]
    l [vowel] => j * / [vowel] [cons]* [vowel] _ [cons]+ [vowel]

vowel-harmony-by-frontness [vowel] propagate:
    [vowel !cent] => [$Depth] / [vowel !cent $Depth] [vowel cent]* _

vowel-mergers:
    {ø, ɤ, ə, ɯ} => {y, ɨ, ɨ, ɨ} // _ $
    {ø, ɯ} => {ə, ə} / _ $

velar-fricative:
    x => h

Romanizer:
    pʼ => p'
    tʼ => t'
    kʼ => k'
    pʰ => ph
    tʰ => th
    kʰ => kh
    ᵐb => mb
    ⁿd => nd
    ᵑg => ng
    m̥ => hm
    n̥ => hn
    ə => e
    j => y
    y => ü
    ɨ => ï
    f => v / {[vowel], [sonor]} _ {[vowel], [sonor]}
