Feature +long
Feature (syllable) stress(*unstressed, primary, secondary)
Feature (syllable) +heavy

Diacritic ː (floating) [+long]
Diacritic ˈ (before) [primary]
Diacritic ˌ (before) [secondary]
Diacritic ² [+heavy]

Class vowel {a, e, i, o, u}
Class stop {p, t, k, b, d, ɡ}
Class liquid {r, l, w, j}
Class cons {@stop, s, m, n, @liquid}

deromanizer:
    {aa, ee, ii, oo, uu} => {aː, eː, iː, oː, uː}
    g => ɡ
    y => j

Syllables:
    {@cons, @liquid, (@cons @liquid // @vowel _), *} {@vowel @cons, @vowel&[+long]} => [+heavy]
    {@cons, @liquid, (@cons @liquid // @vowel _), *} @vowel

stress:
    (
        <syl> => [primary] / _ <syl>&[-heavy] <syl>&[-heavy] $
        Else:
        <syl> => [primary] / _ <syl> $
    )
    Then:
    (
        <syl>&[+heavy !primary] => [secondary] / $ _
        Else:
        <syl>&[!primary] => [secondary] / $ <syl>&[!primary] _
    )
    Then propagate:
        <syl>&[unstressed] => [secondary] / <syl>&[secondary] <syl>&[unstressed] _

Romanizer-before-syncope:
    unchanged

syncope:
    @vowel&[-long unstressed] => *

Syllables:
    (@cons // @vowel _)? @cons? @vowel @cons? @cons?

drop-syllable-features:
    <syl>&[+heavy] => [-heavy]
    <syl>&[secondary] => [unstressed]
